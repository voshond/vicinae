import QtQuick
import QtQuick.Layouts

RowLayout {
    id: root

    required property var args
    required property string icon

    signal valueChanged(int index, string value)
    signal focusSearchInput

    readonly property int maxArgs: 3
    readonly property var visibleArgs: args ? args.slice(0, maxArgs) : []

    spacing: 4

    function validate() {
        var firstRequired = -1;
        for (var i = 0; i < argRepeater.count; i++) {
            var loader = argRepeater.itemAt(i);
            if (!loader || !loader.item)
                continue;
            var arg = root.visibleArgs[i];
            if (arg.required && loader.item.currentValue === "") {
                loader.item.showError = true;
                if (firstRequired === -1) {
                    firstRequired = i;
                    loader.item.forceActiveFocus();
                }
            }
        }
    }

    function setValues(values) {
        for (var i = 0; i < argRepeater.count && i < values.length; i++) {
            var loader = argRepeater.itemAt(i);
            if (!loader || !loader.item)
                continue;
            var val = values[i].value;
            if (loader.item.currentValue !== val)
                loader.item.currentValue = val;
        }
    }

    ViciImage {
        Layout.preferredWidth: 25
        Layout.preferredHeight: 25
        Layout.alignment: Qt.AlignVCenter
        source: root.icon
    }

    Repeater {
        id: argRepeater
        model: root.visibleArgs

        delegate: Loader {
            id: argLoader
            required property int index
            required property var modelData

            readonly property bool isLast: index === root.visibleArgs.length - 1
            readonly property real maxArgWidth: {
                var totalSpacing = root.spacing * (root.visibleArgs.length + 1);
                return Math.max((root.width - 25 - totalSpacing) / root.visibleArgs.length, 60);
            }

            Layout.alignment: Qt.AlignVCenter

            sourceComponent: modelData.type === "dropdown" ? dropdownDelegate : textDelegate

            Component {
                id: textDelegate

                Rectangle {
                    id: textDel
                    property string currentValue: textField.text
                    property bool showError: false

                    implicitWidth: Math.min((textField.text ? textField.contentWidth : textMetrics.advanceWidth) + 16, argLoader.maxArgWidth)
                    implicitHeight: 26
                    radius: 4
                    color: "transparent"
                    border.width: 1
                    border.color: Config.withAlpha(textDel.showError ? "#e53935" : textField.activeFocus ? Theme.accent : Theme.divider, Config.windowOpacity)

                    function forceActiveFocus() {
                        textField.forceActiveFocus();
                    }

                    onCurrentValueChanged: {
                        if (textField.text !== currentValue)
                            textField.text = currentValue;
                    }

                    TextMetrics {
                        id: textMetrics
                        font: textField.font
                        text: textField.text || argLoader.modelData.placeholder || " "
                    }

                    TextInput {
                        id: textField
                        anchors.fill: parent
                        anchors.leftMargin: 8
                        anchors.rightMargin: 8
                        verticalAlignment: TextInput.AlignVCenter
                        font.family: Theme.fontFamily
                        font.pointSize: Theme.regularFontSize
                        color: Theme.foreground
                        clip: true
                        activeFocusOnTab: true
                        echoMode: argLoader.modelData.type === "password" ? TextInput.Password : TextInput.Normal

                        Text {
                            anchors.fill: parent
                            verticalAlignment: Text.AlignVCenter
                            text: argLoader.modelData.placeholder || ""
                            color: Theme.textPlaceholder
                            font: textField.font
                            visible: !textField.text && textField.echoMode !== TextInput.Password
                        }

                        onTextEdited: {
                            textDel.showError = false;
                            root.valueChanged(argLoader.index, text);
                        }

                        Keys.onUpPressed: {
                            commandStack.currentItem.moveUp();
                        }
                        Keys.onDownPressed: {
                            commandStack.currentItem.moveDown();
                        }
                        Keys.onTabPressed: event => {
                            if (argLoader.isLast) {
                                root.focusSearchInput();
                                event.accepted = true;
                            } else {
                                event.accepted = false;
                            }
                        }
                        Keys.onPressed: event => {
                            event.accepted = launcher.forwardKey(event.key, event.modifiers);
                        }
                    }
                }
            }

            Component {
                id: dropdownDelegate

                Rectangle {
                    id: dropdownDel
                    property string currentValue: ""
                    property bool showError: false

                    implicitWidth: Math.min(Math.max(dropdownMetrics.advanceWidth + 36, 80), argLoader.maxArgWidth)
                    implicitHeight: 26
                    radius: 4
                    color: "transparent"
                    border.width: 1
                    border.color: "transparent"

                    function forceActiveFocus() {
                        dropdown.forceActiveFocus();
                    }

                    onCurrentValueChanged: {
                        if (!argLoader.modelData.data)
                            return;
                        for (var i = 0; i < argLoader.modelData.data.length; i++) {
                            if (argLoader.modelData.data[i].value === currentValue) {
                                dropdown.currentItem = {
                                    id: argLoader.modelData.data[i].value,
                                    displayName: argLoader.modelData.data[i].title
                                };
                                return;
                            }
                        }
                    }

                    TextMetrics {
                        id: dropdownMetrics
                        font.family: Theme.fontFamily
                        font.pointSize: Theme.regularFontSize
                        text: dropdown.currentItem ? dropdown.currentItem.displayName : (argLoader.modelData.placeholder || " ")
                    }

                    SearchableDropdown {
                        id: dropdown
                        anchors.fill: parent
                        compact: true
                        activeFocusOnTab: true
                        placeholder: argLoader.modelData.placeholder || ""
                        items: {
                            if (!argLoader.modelData.data)
                                return [];
                            var entries = [];
                            for (var i = 0; i < argLoader.modelData.data.length; i++) {
                                var d = argLoader.modelData.data[i];
                                entries.push({
                                    id: d.value,
                                    displayName: d.title,
                                    iconSource: ""
                                });
                            }
                            return [
                                {
                                    title: "",
                                    items: entries
                                }
                            ];
                        }
                        onActivated: item => {
                            dropdown.currentItem = item;
                            dropdownDel.showError = false;
                            dropdownDel.currentValue = item.id;
                            root.valueChanged(argLoader.index, item.id);
                        }

                        Keys.onTabPressed: event => {
                            if (argLoader.isLast) {
                                root.focusSearchInput();
                                event.accepted = true;
                            } else {
                                event.accepted = false;
                            }
                        }
                        Keys.onPressed: event => {
                            event.accepted = launcher.forwardKey(event.key, event.modifiers);
                        }
                    }
                }
            }
        }
    }

    Item {
        Layout.fillWidth: true
    }
}
