import QtQuick
import QtQuick.Layouts

Item {
    id: root
    required property var host

    readonly property var _alert: root.host.alert ?? ({})
    readonly property bool _hasAlert: Object.keys(_alert).length > 0

    readonly property var platformIcons: ({
            "linux": "linux",
            "macOS": "apple",
            "macOS ": "apple",
            "Windows": "windows11",
            "windows": "windows11"
        })

    component TextLink: RowLayout {
        property string label: ""
        property string url: ""

        spacing: 4

        Text {
            text: parent.label
            color: _linkArea.containsMouse ? Theme.accent : Theme.foreground
            font.pointSize: Theme.regularFontSize
        }

        ViciImage {
            Layout.preferredWidth: 14
            Layout.preferredHeight: 14
            source: Img.builtin("arrow-ne").withFillColor(_linkArea.containsMouse ? Theme.accent : Theme.textMuted)
        }

        MouseArea {
            id: _linkArea
            anchors.fill: parent
            hoverEnabled: true
            cursorShape: Qt.PointingHandCursor
            onClicked: root.host.openUrl(parent.url)
        }
    }

    component SidebarLabel: Text {
        color: Theme.textMuted
        font.pointSize: Theme.smallerFontSize
    }

    Flickable {
        id: flickable
        anchors.fill: parent
        contentWidth: width
        contentHeight: _content.implicitHeight
        clip: true
        flickableDirection: Flickable.VerticalFlick
        boundsBehavior: Flickable.StopAtBounds
        visible: root.host.isReady

        ViciWheelHandler {
            target: flickable
        }

        ColumnLayout {
            id: _content
            width: parent.width
            spacing: 0

            RowLayout {
                Layout.fillWidth: true
                Layout.margins: 25
                spacing: 20

                ViciImage {
                    Layout.preferredWidth: 64
                    Layout.preferredHeight: 64
                    Layout.alignment: Qt.AlignTop
                    source: root.host.iconSource
                }

                ColumnLayout {
                    Layout.fillWidth: true
                    spacing: 4

                    Text {
                        text: root.host.title
                        color: Theme.foreground
                        font.pointSize: Theme.regularFontSize + 4
                        font.bold: true
                        elide: Text.ElideRight
                        Layout.fillWidth: true
                    }

                    RowLayout {
                        spacing: 10

                        RowLayout {
                            spacing: 6

                            ViciImage {
                                Layout.preferredWidth: 16
                                Layout.preferredHeight: 16
                                source: root.host.authorAvatar
                            }

                            Text {
                                text: root.host.authorName
                                color: Theme.textMuted
                                font.pointSize: Theme.smallerFontSize
                            }
                        }

                        Rectangle {
                            width: 1
                            height: 14
                            color: Theme.divider
                        }

                        RowLayout {
                            spacing: 4

                            ViciImage {
                                Layout.preferredWidth: 14
                                Layout.preferredHeight: 14
                                source: Img.builtin("arrow-down-circle").withFillColor(Theme.textMuted)
                            }

                            Text {
                                text: root.host.downloadCount
                                color: Theme.textMuted
                                font.pointSize: Theme.smallerFontSize
                            }
                        }

                        Repeater {
                            model: root.host.platforms

                            Row {
                                spacing: 0
                                visible: (root.platformIcons[modelData] || "") !== ""

                                Rectangle {
                                    visible: index === 0
                                    width: 1
                                    height: 14
                                    color: Theme.divider
                                    anchors.verticalCenter: parent.verticalCenter
                                }

                                Item {
                                    width: index === 0 ? 10 : 5
                                    height: 1
                                }

                                ViciImage {
                                    width: 14
                                    height: 14
                                    anchors.verticalCenter: parent.verticalCenter
                                    source: {
                                        var iconName = root.platformIcons[modelData] || "";
                                        if (iconName === "")
                                            return null;
                                        return Img.builtin(iconName).withFillColor(Theme.textMuted);
                                    }
                                }
                            }
                        }

                        Item {
                            Layout.fillWidth: true
                        }
                    }
                }

                Rectangle {
                    visible: root.host.isInstalled
                    Layout.alignment: Qt.AlignTop
                    Layout.preferredHeight: 30
                    implicitWidth: _badgeLayout.implicitWidth + 16
                    radius: 6
                    color: Qt.rgba(Theme.toastSuccess.r, Theme.toastSuccess.g, Theme.toastSuccess.b, 0.15)

                    RowLayout {
                        id: _badgeLayout
                        anchors.centerIn: parent
                        spacing: 6

                        ViciImage {
                            Layout.preferredWidth: 14
                            Layout.preferredHeight: 14
                            source: Img.builtin("check-circle").withFillColor(Theme.toastSuccess)
                        }

                        Text {
                            text: qsTr("Installed")
                            color: Theme.toastSuccess
                            font.pointSize: Theme.smallerFontSize
                            font.bold: true
                        }
                    }
                }
            }

            Rectangle {
                Layout.fillWidth: true
                height: 1
                color: Theme.divider
            }

            Rectangle {
                id: _alertBox
                visible: root._hasAlert
                Layout.fillWidth: true
                Layout.margins: 20
                Layout.bottomMargin: 0
                implicitHeight: _alertContent.implicitHeight + 20
                radius: 8
                color: Qt.rgba(_alertColor.r, _alertColor.g, _alertColor.b, 0.1)
                border.color: Qt.rgba(_alertColor.r, _alertColor.g, _alertColor.b, 0.3)
                border.width: 1

                readonly property color _alertColor: {
                    const colors = {
                        "success": Theme.toastSuccess,
                        "warning": Theme.toastWarning,
                        "danger": Theme.toastDanger,
                        "muted": Theme.textMuted
                    };
                    return colors[root._alert.type] ?? Theme.textMuted;
                }

                readonly property string _alertIcon: {
                    const icons = {
                        "success": "check-circle",
                        "warning": "warning",
                        "danger": "x-mark-circle",
                        "muted": "question-mark-circle"
                    };
                    return icons[root._alert.type] ?? "question-mark-circle";
                }

                RowLayout {
                    id: _alertContent
                    anchors.fill: parent
                    anchors.margins: 10
                    spacing: 10

                    ViciImage {
                        Layout.preferredWidth: 18
                        Layout.preferredHeight: 18
                        Layout.alignment: Qt.AlignTop
                        source: Img.builtin(_alertBox._alertIcon).withFillColor(_alertBox._alertColor)
                    }

                    ColumnLayout {
                        Layout.fillWidth: true
                        spacing: 6

                        Text {
                            text: root._alert.message ?? ""
                            color: Theme.foreground
                            font.pointSize: Theme.smallerFontSize
                            wrapMode: Text.WordWrap
                            Layout.fillWidth: true
                        }

                        Repeater {
                            model: root._alert.notes ?? []

                            RowLayout {
                                Layout.fillWidth: true
                                spacing: 6

                                Text {
                                    text: "•"
                                    color: Theme.textMuted
                                    font.pointSize: Theme.smallerFontSize
                                }

                                Text {
                                    text: modelData
                                    color: Theme.textMuted
                                    font.pointSize: Theme.smallerFontSize
                                    wrapMode: Text.WordWrap
                                    Layout.fillWidth: true
                                }
                            }
                        }
                    }
                }
            }

            Flickable {
                visible: root.host.hasScreenshots
                Layout.fillWidth: true
                Layout.preferredHeight: 160
                Layout.margins: 20
                contentWidth: _screenshotRow.width
                clip: true
                flickableDirection: Flickable.HorizontalFlick
                boundsBehavior: Flickable.StopAtBounds

                Row {
                    id: _screenshotRow
                    spacing: 12

                    Repeater {
                        model: root.host.screenshots

                        Item {
                            width: 240
                            height: 150

                            ViciImage {
                                anchors.fill: parent
                                source: modelData
                                fillMode: Image.PreserveAspectCrop
                            }

                            Rectangle {
                                anchors.fill: parent
                                color: "transparent"
                                border.color: Config.withAlpha(Theme.divider, Config.windowOpacity)
                                border.width: 1
                                radius: 4
                            }

                            MouseArea {
                                anchors.fill: parent
                                cursorShape: Qt.PointingHandCursor
                                onClicked: _imageViewer.showImage(index, root.host.screenshots)
                            }
                        }
                    }
                }
            }

            Rectangle {
                visible: root.host.hasScreenshots
                Layout.fillWidth: true
                height: 1
                color: Theme.divider
            }

            RowLayout {
                Layout.fillWidth: true
                spacing: 0

                ColumnLayout {
                    Layout.fillWidth: true
                    Layout.preferredWidth: 2
                    Layout.alignment: Qt.AlignTop
                    Layout.margins: 20
                    spacing: 20

                    ColumnLayout {
                        Layout.fillWidth: true
                        spacing: 10

                        Text {
                            text: qsTr("Description")
                            color: Theme.foreground
                            font.pointSize: Theme.regularFontSize
                            font.bold: true
                        }

                        Text {
                            text: root.host.description
                            color: Theme.textMuted
                            font.pointSize: Theme.regularFontSize
                            wrapMode: Text.WordWrap
                            Layout.fillWidth: true
                        }
                    }

                    Rectangle {
                        Layout.fillWidth: true
                        height: 1
                        color: Theme.divider
                    }

                    ColumnLayout {
                        Layout.fillWidth: true
                        spacing: 15

                        Text {
                            text: qsTr("Commands")
                            color: Theme.textMuted
                            font.pointSize: Theme.regularFontSize
                        }

                        Repeater {
                            model: root.host.commands

                            ColumnLayout {
                                Layout.fillWidth: true
                                spacing: 0

                                ColumnLayout {
                                    Layout.fillWidth: true
                                    spacing: 10

                                    RowLayout {
                                        spacing: 10

                                        ViciImage {
                                            Layout.preferredWidth: 20
                                            Layout.preferredHeight: 20
                                            source: modelData.iconSource
                                        }

                                        Text {
                                            text: modelData.title
                                            color: Theme.foreground
                                            font.pointSize: Theme.regularFontSize
                                        }
                                    }

                                    Text {
                                        text: modelData.description
                                        color: Theme.textMuted
                                        font.pointSize: Theme.smallerFontSize
                                        wrapMode: Text.WordWrap
                                        Layout.fillWidth: true
                                        visible: text !== ""
                                    }
                                }

                                Rectangle {
                                    visible: index < root.host.commands.length - 1
                                    Layout.fillWidth: true
                                    Layout.topMargin: 15
                                    height: 1
                                    color: Theme.divider
                                }
                            }
                        }
                    }
                }

                Rectangle {
                    Layout.fillHeight: true
                    width: 1
                    color: Theme.divider
                }

                ColumnLayout {
                    Layout.preferredWidth: 1
                    Layout.alignment: Qt.AlignTop
                    Layout.margins: 15
                    spacing: 15

                    ColumnLayout {
                        visible: (root.host.readmeUrl || "") !== ""
                        spacing: 5

                        SidebarLabel {
                            text: "README"
                        }
                        TextLink {
                            label: qsTr("Open README")
                            url: root.host.readmeUrl || ""
                        }
                    }

                    ColumnLayout {
                        spacing: 5

                        SidebarLabel {
                            text: qsTr("Last update")
                        }
                        Text {
                            text: root.host.lastUpdate
                            color: Theme.foreground
                            font.pointSize: Theme.regularFontSize
                        }
                    }

                    ColumnLayout {
                        visible: root.host.contributors.length > 0
                        spacing: 10

                        SidebarLabel {
                            text: qsTr("Contributors")
                        }

                        Repeater {
                            model: root.host.contributors

                            RowLayout {
                                spacing: 8

                                ViciImage {
                                    Layout.preferredWidth: 16
                                    Layout.preferredHeight: 16
                                    source: modelData.avatar
                                }

                                Text {
                                    text: modelData.name
                                    color: Theme.foreground
                                    font.pointSize: Theme.smallerFontSize
                                }
                            }
                        }
                    }

                    ColumnLayout {
                        visible: root.host.categories.length > 0
                        spacing: 5

                        SidebarLabel {
                            text: qsTr("Categories")
                        }

                        Repeater {
                            model: root.host.categories

                            Text {
                                text: modelData
                                color: Theme.foreground
                                font.pointSize: Theme.regularFontSize
                            }
                        }
                    }

                    ColumnLayout {
                        visible: (root.host.sourceUrl || "") !== ""
                        spacing: 5

                        SidebarLabel {
                            text: qsTr("Source Code")
                        }
                        TextLink {
                            label: qsTr("View Code")
                            url: root.host.sourceUrl || ""
                        }
                    }
                }
            }
        }
    }

    ImageViewer {
        id: _imageViewer
    }

    Connections {
        target: Nav
        function onWindowVisiblityChanged(visible) {
            if (!visible)
                _imageViewer.close();
        }
    }

    Component.onDestruction: _imageViewer.close()
}
